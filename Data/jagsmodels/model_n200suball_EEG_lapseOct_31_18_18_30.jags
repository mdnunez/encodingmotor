
model {
	##########
	#Fixed Parameters
	beta <- .5
	##########
	#Between-EEGsession variability in non-decision time
	tersubsd ~ dgamma(.2,1)

	#Between-EEGsession variability in drift
	deltasubsd ~ dgamma(1,1)

	#Between-EEGsession variability in boundary separation
	alphasubsd ~ dgamma(1,1)

	#Between-EEGsession variability in mind-wandering probability
	probsubsd ~ dgamma(.2,1)

	#Between-EEGsession variability in EEG latency
	EEGsubsd ~ dgamma(.2,1)

	for (f in 1:4) {
		effectult[1,f] ~ dnorm(1,pow(3,-2)) #'Informative' prior
		effectsd[1,f] ~ dgamma(1,1)
	}

	##########
	#Block-level parameters
	##########
	for (k in 1:nconds) {

	        #Condition-level non-decision time
			tercond[1,k] ~ dnorm(.3, pow(.25,-2))

			#Condition-level drift rate
			deltacond[1,k] ~ dnorm(1, pow(2, -2))

			#Condition-level boundary separation
			alphacond[1,k] ~ dnorm(1, pow(.5,-2))

			#Condition-level mind wandering probability
			probcond[1,k] ~ dnorm(.2, pow(.1,-2))

			#Condition-level EEG latency
			EEGcond[1,k] ~ dnorm(.3, pow(.1,-2))

			#Condition-level effects of EEG input on non-decision time, drift rate, and boundary separation
			for (f in 1:4) {
				effectcond[f,k] ~ dnorm(effectult[1,f],pow(effectsd[1,f],-2))
			}
		#EEGsession-level parameters
		for (ses in 1:nses) {
			#EEGsession-level non-decision time
			tersub[k,ses] ~ dnorm(tercond[1,k]
				+ effectcond[1,k]*EEGsub[k,ses],
				pow(tersubsd, -2))T(0,.7)

			#EEGsession-level drift rate
			deltasub[k,ses] ~ dnorm(deltacond[1,k]
				+ effectcond[2,k]*EEGsub[k,ses],
				pow(deltasubsd, -2))T(-9, 9)

			#EEGsession-level boundary separation
			alphasub[k,ses] ~ dnorm(alphacond[1,k]
				+ effectcond[3,k]*EEGsub[k,ses],
				pow(alphasubsd, -2))T(.1,3)

			#EEGsession-level mind wandering probability
        	probsub[k, ses] ~ dnorm(probcond[1,k]
        		+ effectcond[4,k]*EEGsub[k,ses],
        		pow(probsubsd, -2))T(0, 1)
        	DDMprobsub[k, ses] <- 1 - probsub[k, ses]

        	#EEGsession-level EEG latency (impute missing values)
			EEGsub[k, ses] ~ dnorm(EEGcond[1,k],
				pow(EEGsubsd, -2))

		}
	}
	##########
	# Wiener likelihoods
	for (i in 1:N) {
        # Log density for DDM process
        ld_comp[i, 1] <- dlogwiener(y[i],alphasub[condition[i],EEGsession[i]],
        tersub[condition[i],EEGsession[i]],
        beta,
        deltasub[condition[i],EEGsession[i]])

        # Log density for lapse trials (negative max RT to positive max RT)
        ld_comp[i, 2] <- logdensity.unif(y[i], -maxrt[condition[i],EEGsession[i]], maxrt[condition[i],EEGsession[i]])

        # Select one of these two densities (Mixture of nonlapse and lapse trials)
        density[i] <- exp(ld_comp[i, component_chosen[i]] - Constant)
		
        # Generate a likelihood for the MCMC sampler using a trick to maximize density value
        Ones[i] ~ dbern(density[i])

        # Probability of mind wandering trials (lapse trials)
        component_chosen[i] ~ dcat( c(DDMprobsub[condition[i],EEGsession[i]], probsub[condition[i],EEGsession[i]]) )
	}
}
